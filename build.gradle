plugins {
	id 'java'
	id 'maven-publish'
	id 'org.quiltmc.loom' version '1.6.5'
	id 'ploceus' version '1.6-SNAPSHOT'
	id "com.github.breadmoirai.github-release" version "2.4.1"
}

base {
	archivesName = project.archives_base_name
}
version = project.mod_version
group = project.maven_group

loom {
	clientOnlyMinecraftJar()
}

ploceus {
	clientOnlyMappings()
}

repositories {
}

dependencies {
	minecraft "com.mojang:minecraft:${project.minecraft_version}"
	mappings project.ploceus.featherMappings(project.feather_build)
	modImplementation "org.quiltmc:quilt-loader:${project.loader_version}"
	include(implementation "com.formdev:flatlaf:3.4.1")
}

project.sourceSets {
	main {
		java {
			srcDirs += project.rootProject.sourceSets.main.java.srcDirs
		}
		resources {
			srcDirs += project.rootProject.sourceSets.main.resources.srcDirs
		}
	}
}

processResources {
	inputs.properties 'version': version, 'group': project.group

	filesMatching('quilt.mod.json') {
		expand 'version': version, 'group': project.group
	}
}

tasks.withType(JavaCompile).configureEach {
	it.options.encoding = 'UTF-8'
	it.options.release = 8
}

java {
	// Still required by IDEs such as Eclipse and Visual Studio Code
	sourceCompatibility = JavaVersion.VERSION_1_8
	targetCompatibility = JavaVersion.VERSION_1_8

	// Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task if it is present.
	// If you remove this line, sources will not be generated.
	withSourcesJar()

	// If this mod is going to be a library, then it should also generate Javadocs in order to aid with development.
	// Uncomment this line to generate them.
	// withJavadocJar()
}

// If you plan to use a different file for the license, don't forget to change the file name here!
jar {
	from('LICENSE') {
		rename { "${it}_${project.archivesBaseName}"}
	}
}

// Configure the maven publication
publishing {
	publications {
		mavenJava(MavenPublication) {
			from project.components.java
		}
	}

	// See https://docs.gradle.org/current/userguide/publishing_maven.html for information on how to set up publishing.
	repositories {
		// Add repositories to publish to here.
		// Notice: This block does NOT have the same function as the block in the top level.
		// The repositories here will be used for publishing your artifact, not for
		// retrieving dependencies.
	}
}
githubRelease {
	token rootProject.file(".env").text.readLines().get(1).replace("GITHUB=", "").replace("\"", "")
	owner "MCLegoMan"
	repo "mclm_save"
	tagName "${project.mod_version}"
	targetCommitish "${project.minecraft_version}"
	releaseName "[${project.minecraft_version}] ${project.mod_version}"
	generateReleaseNotes false
	body rootProject.file("CHANGELOG.md").text
	draft false
	if (project.mod_version.contains("release")) prerelease false
	else prerelease true
	releaseAssets tasks.remapJar
	allowUploadToExisting.set false
	overwrite false
	dryRun false
	apiEndpoint "https://api.github.com"
	client
}